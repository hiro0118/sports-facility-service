plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id "io.freefair.lombok" version "8.0.1"
}

group = 'com.hiro0118'
version = '1.0'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

tasks.named('test') {
	useJUnitPlatform()
}

sourceSets {
	integration {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration/java')
		}
		resources {
			srcDirs file('src/integration/resources')
		}
	}
}

configurations {
	integrationImplementation.extendsFrom testImplementation
	integrationRuntime.extendsFrom testRuntime
}

tasks.register('integration', Test) {
	useJUnitPlatform()
	testClassesDirs = sourceSets.integration.output.classesDirs
	classpath = sourceSets.integration.runtimeClasspath
	outputs.upToDateWhen { false }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'mysql:mysql-connector-java:8.0.18'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.2'
}

allprojects {
	tasks.withType(Copy).configureEach {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	}
}
